BACKUP ~fadingpromises/backup~
//AUTHOR ~berelinde@gmail.com~
SUPPORT ~http://www.shsforums.net/forum/528-fading-promises/~

//MODDER

VERSION ~v9 pre~ 

README ~fadingpromises/readmefadingpromises.html~

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter THEN BEGIN // check to make this happen only once per install

    ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter
    END

  ACTION_DEFINE_ARRAY charsetsReloadArray BEGIN setup-fadingpromises END

    LAF ~HANDLE_CHARSETS~
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = EVAL ~%MOD_FOLDER%~
        reload_array = charsetsReloadArray
    END
    LOAD_TRA ~fadingpromises/%LANGUAGE%/setup-fadingpromises-ee.tra~
    
  END
END

AUTO_TRA ~fadingpromises/%s~

LANGUAGE ~English~  ~english~ ~fadingpromises/english/setup-fadingpromises.tra~
LANGUAGE ~Espanol~  ~spanish~ ~fadingpromises/spanish/setup-fadingpromises.tra~
LANGUAGE ~Deutsch~  ~german~  ~fadingpromises/german/setup-fadingpromises.tra~
LANGUAGE ~Italiano~ ~italian~  ~fadingpromises/italian/setup-fadingpromises.tra~
LANGUAGE ~Russian~ ~russian~  ~fadingpromises/russian/setup-fadingpromises.tra~
LANGUAGE ~Francais~ ~french~  ~fadingpromises/french/setup-fadingpromises.tra~

BEGIN @0

/* STATE.IDS patching adds custom IsValidForPartyDialogue state */
  APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

ACTION_IF GAME_IS ~bg2 tob bgt~ THEN BEGIN
  // Fixpack ids fixes
  INCLUDE ~fadingpromises/lib/ids.tph~
  CLEAR_IDS_MAP
END

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  ADD_JOURNAL @25 @53 @75 @141 @144 @147 @161 @162 @163 @164 @165 @166 @167 @168 @169 @170 @171 @172 @173 @174 @175 @176 @191 @210 @217 @10000 USING ~fadingpromises/%LANGUAGE%/b!aidan.tra~

  ADD_JOURNAL @10000 USING ~fadingpromises/%LANGUAGE%/setup-fadingpromises.tra~
END
  
/* compiling dialogue */
COMPILE EVALUATE_BUFFER ~fadingpromises/dialogue/b!aidan.d~
COMPILE ~fadingpromises/dialogue/b!selma.d~
COMPILE ~fadingpromises/dialogue/b!udtrap04.d~

/* compiling script */
COMPILE ~fadingpromises/script/b!aidanm.baf~
COMPILE ~fadingpromises/script/b!aidann.baf~
COMPILE ~fadingpromises/script/b!aidans.baf~
COMPILE ~fadingpromises/script/b!selmas.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~fadingpromises/script/b!baldur.baf~
EXTEND_TOP ~ar0800.bcs~ ~fadingpromises/script/b!ar0800.baf~
EXTEND_BOTTOM ~ar0900.bcs~ ~fadingpromises/script/b!ar0900.baf~
EXTEND_BOTTOM ~ar1401.bcs~ ~fadingpromises/script/b!ar1401.baf~
EXTEND_BOTTOM ~cleanse.bcs~ ~fadingpromises/script/b!cleanse.baf~

/* items */ 
COPY ~fadingpromises/item/b!minhp.itm~ ~override~ 
COPY ~fadingpromises/item/b!amacec.bam~ ~override~
COPY ~fadingpromises/item/b!amacei.bam~ ~override~
COPY ~fadingpromises/item/b!amace.itm~ ~override~
  SAY NAME1 @1
  SAY NAME2 @1
  SAY UNIDENTIFIED_DESC @2
  SAY DESC @3
 

/* sound */
COPY ~fadingpromises/music~ ~override~

LAF HANDLE_AUDIO
  STR_VAR
  audio_path = ~fadingpromises/audio~
  oggdec_path = ~fadingpromises/audio~
  sox_path = ~fadingpromises/audio~
END


COPY_EXISTING ~songlist.2da~ ~override~
  SET_2DA_ENTRY 0 2 3 ~Blank.mus~

/* creatures */
COPY ~fadingpromises/bmp~ ~override~

COPY_EXISTING ~kamir.cre~ ~override/b!aidan.cre~
  ADD_CRE_ITEM ~sw2h01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~b!minhp~ #0 #0 #0 ~IDENTIFIED~ ~AMULET~
  ADD_CRE_ITEM ~ghost~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
  WRITE_ASCII 0x248 ~b!aidans~ #8 // assign override script
  WRITE_ASCII 0x250 ~fighter2~ #8 // assign class script
  WRITE_ASCII 0x2cc ~b!aidan~ #8 // assign dialogue file
  WRITE_ASCII 0x280 ~b!aidan~ #32 // assign DV
  WRITE_ASCII 0X34 ~b!aidans~ #8 //small portrait
  WRITE_BYTE 0x2c 105  //metal color (white)
  WRITE_BYTE 0x2d 105  //minor color (white)
  WRITE_BYTE 0x2e 105  //major color (white)
  WRITE_BYTE 0x2f 13   //skin color (light pure silver)
  WRITE_BYTE 0x30 105  //leather color (white)
  WRITE_BYTE 0x31 105  //armor color (white)
  WRITE_BYTE 0x32 10   //hair color (light pure gold)
  SAY NAME1 @4
  SAY NAME2 @4
  SAY INITIAL_MEETING @5 

//COPY_EXISTING ~UDTRAP04.cre~ ~override~
//  ADD_CRE_ITEM ~b!amace~ #0 #0 #0 ~IDENTIFIED~ ~INV16~

COPY_EXISTING ~CLMOM.cre~ ~override/b!selma.cre~
  ADD_CRE_ITEM ~misc07~ #5 #0 #0 ~IDENTIFIED~ ~INV16~
  WRITE_ASCII 0x248 ~b!selmas~ #8 // override
  WRITE_ASCII 0x2cc ~b!selma~ #8 // dialogue
  WRITE_ASCII 0x280 ~b!selma~ #32 //DV
  WRITE_ASCII 0X34 ~b!selmas~ #8 //small portrait
  WRITE_BYTE 0x2c 10   //metal color (light pure gold)
  WRITE_BYTE 0x2d 66  //minor color (faded black)
  WRITE_BYTE 0x2e 19   //major color (dark rose red)
  WRITE_BYTE 0x2f 12   //skin color (light carnation pink
  WRITE_BYTE 0x30 99   //leather color (dark gold)
  WRITE_BYTE 0x31 81   //armor color (faded gold)
  WRITE_BYTE 0x32 02   //hair color (dark gold)
  SAY NAME1 @6
  SAY NAME2 @6

/* movie */
COPY ~fadingpromises/movie/b!aidtxt.mos~ ~override~
COPY ~fadingpromises/movie/b!aidtxt.2da~
     ~override/b!aidtxt.2da~
    REPLACE ~9599~ @7

/* Compatibility with Vic's BG2 Improvements (Victor's Improvements Pack http://forums.blackwyrmlair.net/index.php?showforum=87) */ 

ACTION_IF FILE_EXISTS_IN_GAME ~lyddial.dlg~ THEN BEGIN 
    COPY_EXISTING ~udtrap04.cre~ ~override/viclydia.cre~ 
    COPY ~fadingpromises/creatures/udtrap04.cre~ ~override~ 
        ADD_CRE_ITEM ~b!amace~ #0 #0 #0 ~IDENTIFIED~ ~INV16~
    
    COPY_EXISTING ~dwface04.bcs~ ~override~ 
        DECOMPILE_AND_PATCH BEGIN  
            REPLACE_TEXTUALLY CASE_INSENSITIVE ~TriggerActivation("Face4",FALSE)~ ~~ 
        END 
        
    EXTEND_BOTTOM ~dwface04.bcs~ ~fadingpromises/script/b!dwface04.baf~ 
END ELSE BEGIN
    COPY_EXISTING ~UDTRAP04.cre~ ~override~
        ADD_CRE_ITEM ~b!amace~ #0 #0 #0 ~IDENTIFIED~ ~INV16~
END
